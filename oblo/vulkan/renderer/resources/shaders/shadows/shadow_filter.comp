#version 460

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_control_flow_attributes : enable

layout(binding = 0, rgba8) uniform restrict readonly image2D t_InSource;
layout(binding = 1, rgba8) uniform restrict writeonly image2D t_OutFiltered;

const uint g_PassIndex = SHADOW_FILTER_PASS_INDEX;
const uint g_Stride = 1u << g_PassIndex;

// We run 8x8 groups
const uint g_ThreadLocalSize = 8;

// We only tap 3 pixels: the center, 1 at the left and 1 at the right, but each pass has different stride
const uint g_TapsPerPixel = 3;
const uint g_TapRadius = g_TapsPerPixel / 2;

const uint g_SharedCacheSize = g_ThreadLocalSize + 2 * g_TapRadius * g_Stride;
const uint g_LoadsPerThread = (g_SharedCacheSize + g_ThreadLocalSize - 1) / g_ThreadLocalSize;

shared float g_shadowCache[g_SharedCacheSize][g_SharedCacheSize];

ivec2 apply_offset_clamp(in ivec2 pixel, in uvec2 offset, in uvec2 resolution)
{
    return ivec2(min(int(resolution.x), int(pixel.x + offset.x)), min(int(resolution.y), int(pixel.y + offset.y)));
}

layout(local_size_x = g_ThreadLocalSize, local_size_y = g_ThreadLocalSize, local_size_z = 1) in;

void main()
{
    const uvec2 resolution = imageSize(t_InSource);

    const uvec2 firstGroupPixel = gl_WorkGroupID.xy * gl_WorkGroupSize.xy;
    const ivec2 firstPixelToLoad = max(ivec2(0), ivec2(firstGroupPixel) - ivec2(g_Stride));

    [[unroll]] for (uint loadY = 0; loadY < g_LoadsPerThread; ++loadY)
    {
        [[unroll]] for (uint loadX = 0; loadX < g_LoadsPerThread; ++loadX)
        {
            const uvec2 offset = gl_WorkGroupSize.xy * uvec2(loadX, loadY) + gl_LocalInvocationID.xy;

            if (offset.x < g_SharedCacheSize && offset.y < g_SharedCacheSize)
            {
                const ivec2 coords = apply_offset_clamp(firstPixelToLoad, offset, resolution);
                const vec4 pixel = imageLoad(t_InSource, coords);

                g_shadowCache[offset.y][offset.x] = pixel.x;
            }
        }
    }

    memoryBarrierShared();
    barrier();

    const uvec2 screenPos = gl_GlobalInvocationID.xy;

    if (screenPos.x >= resolution.x || screenPos.y >= resolution.y)
    {
        return;
    }

    const uvec2 center = uvec2(g_Stride) + gl_LocalInvocationID.xy;

    // const float shadowCenter = g_shadowCache[center.y][center.x];

    float result = 0.f;

    const int k = int(g_TapRadius);

    [[unroll]] for (int y = -k; y <= k; ++y)
    {
        [[unroll]] for (int x = -k; x <= k; ++x)
        {
            const float shadow = g_shadowCache[center.y + y * g_Stride][center.x + x * g_Stride];
            result += shadow;
        }
    }

    result /= float(g_TapsPerPixel * g_TapsPerPixel);

    imageStore(t_OutFiltered, ivec2(screenPos), vec4(result, 0, 0, 1));
}