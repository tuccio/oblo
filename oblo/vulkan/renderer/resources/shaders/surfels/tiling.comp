#version 460

#extension GL_GOOGLE_include_directive : require
#extension GL_EXT_buffer_reference : require
#extension GL_ARB_gpu_shader_int64 : require
#extension GL_EXT_shader_8bit_storage : require
#extension GL_EXT_control_flow_attributes : require
#extension GL_KHR_shader_subgroup_arithmetic : require
#extension GL_KHR_shader_subgroup_basic : require
#extension GL_KHR_shader_subgroup_ballot : require
#extension GL_KHR_shader_subgroup_shuffle : require

#include <ecs/entity_buffer>
#include <renderer/debug/printf>
#include <renderer/instances>
#include <renderer/math>
#include <renderer/meshes/mesh_attributes>
#include <renderer/meshes/mesh_data>
#include <renderer/meshes/mesh_indices>
#include <renderer/meshes/mesh_table>
#include <renderer/random/random>
#include <renderer/transform>
#include <surfels/buffers/surfel_data_r>
#include <surfels/buffers/surfel_grid_data_r>
#include <surfels/buffers/surfel_grid_r>
#include <surfels/buffers/surfel_lighting_data_in_r>
#include <surfels/reduction>
#include <visibility/visibility_buffer>
#include <visibility/visibility_utils>

const uint g_TileSize = TILE_SIZE;

layout(push_constant) uniform c_PushConstants
{
    uint randomSeed;
}
g_Constants;

layout(std430, binding = 0) restrict writeonly buffer b_OutTileCoverage
{
    surfel_tile_data g_TileData[];
};

layout(binding = 10, rg32ui) uniform restrict readonly uimage2D t_InVisibilityBuffer;

layout(std430, binding = SURFEL_LAST_USAGE_BINDING) restrict writeonly buffer b_SurfelsLastUsage
{
    uint g_SurfelLastUsage[];
};

layout(binding = 16) uniform b_CameraBuffer
{
    camera_buffer g_Camera;
};

layout(local_size_x = g_TileSize, local_size_y = g_TileSize, local_size_z = 1) in;

bool read_spawn_data(
    in ivec2 screenPos, in uvec2 resolution, out surfel_spawn_data spawnData, out vec3 positionWS, out vec3 normalWS);

uint count_pixels_with_fragments(in bool hasFragment)
{
    const uvec4 hasFragmentBallot = subgroupBallot(hasFragment);
    return subgroupBallotBitCount(hasFragmentBallot);
}

shared surfel_tile_data g_GroupCandidates[OBLO_SUBGROUP_SIZE];
shared uint g_PixelsWithFragments[OBLO_SUBGROUP_SIZE];
shared float g_WeightSums[OBLO_SUBGROUP_SIZE];

bool read_spawn_data(
    in ivec2 screenPos, in uvec2 resolution, out surfel_spawn_data spawnData, out vec3 positionWS, out vec3 normalWS)
{
    // Parse the visibility buffer to find which triangle we are processing
    const uvec4 visBufferData = imageLoad(t_InVisibilityBuffer, screenPos);

    visibility_buffer_data vb;

    if (!visibility_buffer_parse(visBufferData.xy, vb))
    {
        return false;
    }

    // Read the instance data we need
    const mesh_handle mesh = OBLO_INSTANCE_DATA(vb.instanceTableId, i_MeshHandles, vb.instanceId);
    const transform transform = OBLO_INSTANCE_DATA(vb.instanceTableId, i_TransformBuffer, vb.instanceId);

    triangle triangleWS;
    vec3 triangleNormal[3];

    // Read the mesh data
    const mesh_table meshTable = mesh_table_fetch(mesh);
    const uvec3 vertexIndices = mesh_get_meshlet_indices(meshTable, mesh, vb.meshletId, vb.meshletTriangleId);

    // Read the vertex data and transform everything in world space, we do lighting calculation in that space
    [[unroll]] for (uint i = 0; i < 3; ++i)
    {
        const uint vertexId = vertexIndices[i];

        const vec3 vertexPosition = mesh_get_position(meshTable, vertexId);
        triangleWS.v[i] = (transform.localToWorld * vec4(vertexPosition, 1)).xyz;

        const vec3 vertexNormal = mesh_get_normal(meshTable, vertexId);
        triangleNormal[i] = vertexNormal;
    }

    barycentric_coords bc;

    // Cast a ray from the camera to the near plane and calculate the distance of the ray hit to the plane on the
    // triangle in world space, we use that to derive the position in world space
    const vec2 ndc = screen_to_ndc(screenPos, resolution);
    const ray cameraRay = visibility_calculate_camera_ray(g_Camera, ndc);

    float intersectionDistance;

    // Really the plan should be hitting here, since we already know the triangle was rendered by the rasterizer, we
    // mostly want to calculate at what distance it does
    if (!distance_from_triangle_plane(cameraRay, triangleWS, intersectionDistance))
    {
        return false;
    }

    positionWS = ray_point_at(cameraRay, intersectionDistance);
    barycentric_calculate(bc, triangleWS.v, positionWS);

    const vec3 N = barycentric_interpolate(bc, triangleNormal);
    normalWS = normalize(mat3(transform.normalMatrix) * N);

    const ecs_entity entity = OBLO_INSTANCE_DATA(vb.instanceTableId, i_EntityIdBuffer, vb.instanceId);

    spawnData.entity = entity;
    spawnData.packedMeshletAndTriangleId = visibility_buffer_get_packed_meshlet_ids(visBufferData.xy);
    spawnData.barycentricU = bc.lambda.x;
    spawnData.barycentricV = bc.lambda.y;

    return all(greaterThanEqual(bc.lambda, vec3(0)));
}

float gather_coverage_from_grid(in vec3 position, in vec3 normal, out vec3 gridAverageIrradiance)
{
    gridAverageIrradiance = vec3(0);
    float weightSum = 0;

    const ivec3 cell = surfel_grid_find_cell(g_SurfelGridHeader, position);

    if (surfel_grid_has_cell(g_SurfelGridHeader, cell))
    {
        const uint currentTimestamp = g_SurfelGridHeader.currentTimestamp;
        const uint cellIndex = surfel_grid_cell_index(g_SurfelGridHeader, cell);

        const surfel_grid_cell gridCell = g_SurfelGridCells[cellIndex];

        surfel_grid_cell_iterator cellIt = surfel_grid_cell_iterator_begin(gridCell);

        for (; surfel_grid_cell_iterator_has_next(cellIt); surfel_grid_cell_iterator_advance(cellIt))
        {
            const uint surfelId = surfel_grid_cell_iterator_get(cellIt);
            const surfel_data surfel = g_SurfelData[surfelId];

            gridAverageIrradiance += g_InSurfelsLighting[surfelId].irradiance;

            const vec3 surfelPosition = surfel_data_world_position(surfel);
            const vec3 surfelNormal = surfel_data_world_normal(surfel);

            const vec3 pToS = surfelPosition - position;

            const float distance2 = dot(pToS, pToS);

            const float radius2 = surfel.radius * surfel.radius;

            // We allow influences up to this distance
            const float threshold = SURFEL_CONTRIBUTION_THRESHOLD_SQR * radius2;

            if (distance2 <= threshold)
            {
                const float angleWeight = max(dot(surfelNormal, normal), 0);
                const float weight = angleWeight * (1 - distance2 / threshold);
                weightSum += weight;

                g_SurfelLastUsage[surfelId] = currentTimestamp;
            }
        }

        gridAverageIrradiance /= max(1, surfel_grid_cell_iterator_count(cellIt));
    }

    return weightSum;
}

void main()
{
    const ivec2 screenPos = ivec2(gl_WorkGroupID.xy * gl_WorkGroupSize.xy + gl_LocalInvocationID.xy);
    const uvec2 resolution = imageSize(t_InVisibilityBuffer);

    const bool withinScreenBounds = screenPos.x < resolution.x && screenPos.y < resolution.y;

    // When nothing is in the visibility buffer, we will fall back to the skybox. These pixels should not really
    // matter for the coverage, for now we assign an arbitrary coverage though.
    float weightSum = 0;
    vec3 positionWS;
    vec3 normalWS;
    vec3 gridAverageIrradiance = vec3(0);

    surfel_spawn_data spawnData;

    const bool hasGeometry =
        withinScreenBounds && read_spawn_data(screenPos, resolution, spawnData, positionWS, normalWS);

    if (!hasGeometry)
    {
        spawnData = surfel_spawn_data_invalid();
        weightSum = NO_SURFELS_NEEDED;
    }
    else
    {
        weightSum = gather_coverage_from_grid(positionWS, normalWS, gridAverageIrradiance);
    }

    const float selfSum = subgroupAdd(weightSum);
    const uint selfPixels = subgroupAdd(hasGeometry ? 1 : 0);

    memoryBarrierShared();
    barrier();

    uint workgroupSeed = random_seed(g_Constants.randomSeed, gl_WorkGroupID.y * gl_WorkGroupSize.x + gl_WorkGroupID.x);
    uint subgroupSeed = random_seed(workgroupSeed, gl_SubgroupID);

    // First process within the subgroup
    {
        const uint bestCandidateIdx = find_lowest_within_subgroup_rand(weightSum, subgroupSeed);

        const uint subgroupOutIndex = (hash_pcg(workgroupSeed) + gl_SubgroupID) % OBLO_SUBGROUP_SIZE;

        if (bestCandidateIdx == gl_SubgroupInvocationID)
        {
            surfel_tile_data tileData;
            tileData.worstPixelCoverage = weightSum;
            tileData.irradianceInit[0] = gridAverageIrradiance.x;
            tileData.irradianceInit[1] = gridAverageIrradiance.y;
            tileData.irradianceInit[2] = gridAverageIrradiance.z;
            tileData.spawnData = spawnData;

            g_GroupCandidates[subgroupOutIndex] = tileData;

            g_WeightSums[subgroupOutIndex] = selfSum;
            g_PixelsWithFragments[subgroupOutIndex] = selfPixels;
        }
    }

    memoryBarrierShared();
    barrier();

    // Then let a single subgroup continue
    if (gl_SubgroupID == 0)
    {
        const surfel_tile_data candidate = g_GroupCandidates[gl_SubgroupInvocationID];

        const float subgroupsSum = g_WeightSums[gl_SubgroupInvocationID];
        const uint subgroupsPixels = g_PixelsWithFragments[gl_SubgroupInvocationID];

        const float bestCandidateValue = subgroupMin(candidate.worstPixelCoverage);
        const float sum = subgroupAdd(subgroupsSum);
        const uint pixels = subgroupAdd(subgroupsPixels);

        // We can use the regular (and faster) overload of find_lowest_within_subgroup, since we shuffled the
        // results in shared memory
        const uint bestCandidateIdx = find_lowest_within_subgroup(candidate.worstPixelCoverage);

        const surfel_tile_data result = g_GroupCandidates[bestCandidateIdx];

        surfel_tile_data outputTile;

        outputTile.worstPixelCoverage = result.worstPixelCoverage;
        outputTile.irradianceInit[0] = result.irradianceInit[0];
        outputTile.irradianceInit[1] = result.irradianceInit[1];
        outputTile.irradianceInit[2] = result.irradianceInit[2];
        outputTile.spawnData = result.spawnData;

        if (subgroupElect())
        {
            const uint globalIndex = gl_WorkGroupID.y * gl_NumWorkGroups.x + gl_WorkGroupID.x;
            g_TileData[globalIndex] = outputTile;
        }
    }
}